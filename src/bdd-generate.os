//----------------------------------------------------------
//This Source Code Form is subject to the terms of the
//Mozilla Public License, v.2.0. If a copy of the MPL
//was not distributed with this file, You can obtain one
//at http://mozilla.org/MPL/2.0/.
//----------------------------------------------------------

//////////////////////////////////////////////////////////////////
//
// Объект-помощник для генерации файла шагов для Gherkin-спецификаций
//
//////////////////////////////////////////////////////////////////

#Использовать logos
#Использовать asserts
// #Использовать strings

Перем Лог;
Перем ЧитательГеркин;

Перем ВозможныеТипыШагов;
Перем ВозможныеКлючиПараметров;
Перем ПредставленияТиповПараметров;
Перем РегулярныеВыражения;

////////////////////////////////////////////////////////////////////
//{ Программный интерфейс

Функция СоздатьФайлРеализацииШагов(ФайлФичи) Экспорт
	Лог.Отладка("Подготовка к генерации шагов спецификации "+ФайлФичи.ПолноеИмя);
	Ожидаем.Что(ФайлФичи, "Ожидали, что файл фичи будет передан как файл, а это не так").ИмеетТип("Файл");

	Лог.Отладка("Читаю фичу");

	РезультатыРазбора = ЧитательГеркин.ПрочитатьФайлСценария(ФайлФичи);

	НаборАдресовШагов = Новый Структура;

	ФайлШагов = ПолучитьФайлШагов(ФайлФичи);

	Если Не ФайлШагов.Существует() Тогда
		ПодготовитьКаталогФайловШагов(ФайлФичи, ФайлШагов);
	КонецЕсли;

	ЗаполнитьФайлШагов(ФайлФичи, РезультатыРазбора, ФайлШагов, НаборАдресовШагов);

	Возврат ФайлШагов;
КонецФункции

Функция ИмяЛога() Экспорт
	Возврат "bdd";
КонецФункции

//}

////////////////////////////////////////////////////////////////////
//{ Реализация

Процедура ПодготовитьКаталогФайловШагов(ФайлФичи, ФайлШагов)
	КаталогШагов = Новый Файл(ФайлШагов.Путь);
	Если Не КаталогШагов.Существует() Тогда
		Лог.Отладка("Каталог шагов не существует. Создаю новый. "+КаталогШагов.ПолноеИмя);
		СоздатьКаталог(КаталогШагов.ПолноеИмя);
	Иначе
		Лог.Отладка("Каталог шагов уже существует."+КаталогШагов.ПолноеИмя);
	КонецЕсли;

КонецПроцедуры

Процедура ЗаполнитьФайлШагов(ФайлФичи, РезультатыРазбора, ФайлШагов, НаборАдресовШагов)
	ЭтоПервичнаяГенерация = Не ФайлШагов.Существует();

	Если ЭтоПервичнаяГенерация Тогда
		Лог.Информация("Начинаю генерацию шагов для спецификации "+ФайлФичи.ПолноеИмя);
	Иначе
		Лог.Информация("Выполняю перегенерацию шагов для спецификации "+ФайлФичи.ПолноеИмя);
	КонецЕсли;

	ДеревоФич = РезультатыРазбора.ДеревоФич;
		Ожидаем.Что(ДеревоФич, "Ожидали, что дерево фич будет передано как дерево значений, а это не так").ИмеетТип("ДеревоЗначений");

	Буфер = Новый Массив;

	ОписаниеЗаписываемогоФайла = Новый Структура;
	ОписаниеЗаписываемогоФайла.Вставить("Буфер", Буфер);
	ОписаниеЗаписываемогоФайла.Вставить("ФайлШагов", ФайлШагов);
	ОписаниеЗаписываемогоФайла.Вставить("ЭтоПервичнаяГенерация", ЭтоПервичнаяГенерация);

	ТаблицаМодуляШагов = Неопределено;
	НаборМетодовМодуляШагов = Неопределено;
	Если Не ЭтоПервичнаяГенерация Тогда
		ТаблицаМодуляШагов = ПрочитатьФайлВТаблицуМодуляШагов(ФайлШагов);
		НаборМетодовМодуляШагов = ПолучитьЭкспортныеМетоды(ТаблицаМодуляШагов);
	КонецЕсли;
	ОписаниеЗаписываемогоФайла.Вставить("ТаблицаМодуляШагов", ТаблицаМодуляШагов);
	ОписаниеЗаписываемогоФайла.Вставить("НаборМетодовМодуляШагов", НаборМетодовМодуляШагов);

	Если ЭтоПервичнаяГенерация Тогда
		ЗаписатьПеременныеВТелоФайлаШагов(Буфер);
	КонецЕсли;

	ЗаписатьОписанияШаговВФайлШагов(ОписаниеЗаписываемогоФайла, ДеревоФич.Строки[0], НаборАдресовШагов);

	Если Не ЭтоПервичнаяГенерация Тогда
		ЗаписатьМетодыВФайл(ТаблицаМодуляШагов, НаборМетодовМодуляШагов, Буфер);
		ЗаполнитьНаборАдресовШаговЭкспортнымиМетодами(НаборАдресовШагов, НаборМетодовМодуляШагов);//TODO для исключения перезаписи уже существующих шагов
	КонецЕсли;
	РекурсивноЗаписатьШагиВФайл(ОписаниеЗаписываемогоФайла, ДеревоФич.Строки[0], НаборАдресовШагов);

	Если Не ЭтоПервичнаяГенерация Тогда
		ЗаписатьОставшеесяТелоВФайл(ТаблицаМодуляШагов, НаборМетодовМодуляШагов, Буфер);
	КонецЕсли;

	НовыйФайл = Новый Файл(ВременныеФайлы.СоздатьФайл("os"));
	ЗаписатьБуферВФайл(Буфер, НовыйФайл);

	КопироватьФайл(НовыйФайл.ПолноеИмя, ФайлШагов.ПолноеИмя);

	Лог.Информация("Генерация завершена.");
КонецПроцедуры

Функция ПрочитатьФайлВТаблицуМодуляШагов(ФайлШагов)
	Если Не ФайлШагов.Существует() Тогда
		ВызватьИсключение "Файл исполнителя шагов не найден."+ФайлШагов.ПолноеИмя;
	КонецЕсли;

	Рез = Новый ТаблицаЗначений;
	Рез.Колонки.Добавить("Строка");

	ЧтениеТекста = Новый ЧтениеТекста;
	ЧтениеТекста.Открыть(ФайлШагов.ПолноеИмя,"UTF-8");

	Строка = ЧтениеТекста.ПрочитатьСтроку();
	Пока Строка <> Неопределено Цикл

		НоваяСтрока        = Рез.Добавить();
		НоваяСтрока.Строка    = Строка;

		Строка = ЧтениеТекста.ПрочитатьСтроку();
	КонецЦикла;

	ЧтениеТекста.Закрыть();

	Возврат Рез;
КонецФункции

Функция ПолучитьЭкспортныеМетоды(ТаблицаМодуляШагов)
	ТаблицаМетодов = Новый ТаблицаЗначений;
	ТаблицаМетодов.Колонки.Добавить("ИмяМетода");
	ТаблицаМетодов.Колонки.Добавить("НомерСтрокиНачала");
	ТаблицаМетодов.Колонки.Добавить("НомерСтрокиБлокаДоМетода");
	ТаблицаМетодов.Колонки.Добавить("НомерСтрокиКонец");

	КонецПредыдущегоБлока = 0;
	КонецПоследнегоМетода = -1;
	Для Счетчик = 0 По ТаблицаМодуляШагов.Количество() - 1 Цикл
		Строка = ТаблицаМодуляШагов[Счетчик].Строка;
		КоллекцияСовпадений = РегулярныеВыражения.НачалоЭкспортногоМетода.НайтиСовпадения(Строка);
		Если КоллекцияСовпадений.Количество() = 0 Тогда
			Продолжить;
		КонецЕсли;

		НоваяСтрокаОписанияМетода = ТаблицаМетодов.Добавить();
		ИмяМетода = КоллекцияСовпадений[0].Группы[2].Значение;
		НоваяСтрокаОписанияМетода.ИмяМетода = ИмяМетода;
		НоваяСтрокаОписанияМетода.НомерСтрокиНачала = Счетчик;
		НоваяСтрокаОписанияМетода.НомерСтрокиБлокаДоМетода = КонецПредыдущегоБлока;

		Пока Истина Цикл
			Счетчик = Счетчик + 1;
			Если Счетчик >= ТаблицаМодуляШагов.Количество() Тогда
				Прервать;
			КонецЕсли;
			Строка = ТаблицаМодуляШагов[Счетчик].Строка;
			КоллекцияСовпадений = РегулярныеВыражения.КонецМетода.НайтиСовпадения(Строка);
			Если КоллекцияСовпадений.Количество() = 0 Тогда
				Продолжить;
			КонецЕсли;

			НоваяСтрокаОписанияМетода.НомерСтрокиКонец = Счетчик;

			КонецПоследнегоМетода = Счетчик;
			КонецПредыдущегоБлока = Счетчик + 1;
			Прервать;
		КонецЦикла;

	КонецЦикла;

	Для каждого Строка Из ТаблицаМетодов Цикл
		Лог.Отладка(СтрШаблон("Получили имя экспортного метода в файле шагов <%1>", Строка.ИмяМетода));
	КонецЦикла;
	Возврат ТаблицаМетодов;
КонецФункции // ПолучитьЭкспортныеМетоды(ТаблицаМодуляШагов)

Процедура ЗаписатьПеременныеВТелоФайлаШагов(Буфер)
	Буфер.Добавить("// Реализация шагов BDD-фич/сценариев c помощью фреймворка https://github.com/artbear/1bdd");
	Буфер.Добавить("");
	Буфер.Добавить("Перем БДД; //контекст фреймворка 1bdd");
	Буфер.Добавить("");
КонецПроцедуры

Процедура ЗаписатьОписанияШаговВФайлШагов(ОписаниеЗаписываемогоФайла, Узел, НаборАдресовШагов)
	Буфер = ОписаниеЗаписываемогоФайла.Буфер;
	ЭтоПервичнаяГенерация = ОписаниеЗаписываемогоФайла.ЭтоПервичнаяГенерация;
	ТаблицаМодуляШагов = ОписаниеЗаписываемогоФайла.ТаблицаМодуляШагов;
	НаборМетодовМодуляШагов = ОписаниеЗаписываемогоФайла.НаборМетодовМодуляШагов;
	ФайлШагов = ОписаниеЗаписываемогоФайла.ФайлШагов;

	Если Не ЭтоПервичнаяГенерация Тогда
		ОписаниеМетода = НаборМетодовМодуляШагов.Найти(ЧитательГеркин.НаименованиеФункцииПолученияСпискаШагов(), "ИмяМетода");
		Если ОписаниеМетода = Неопределено Тогда
			ВызватьИсключение СтрШаблон("Не найден метод %2 для файла шагов <%1>", ФайлШагов.ПолноеИмя, ЧитательГеркин.НаименованиеФункцииПолученияСпискаШагов());
		КонецЕсли;
		ВывестиВБуферСтрокиТаблицы(Буфер, ТаблицаМодуляШагов, ОписаниеМетода.НомерСтрокиБлокаДоМетода, ОписаниеМетода.НомерСтрокиНачала-1);
	КонецЕсли;

	Буфер.Добавить("// Метод выдает список шагов, реализованных в данном файле-шагов");
	Буфер.Добавить(СтрШаблон("Функция %1(КонтекстФреймворкаBDD) Экспорт", ЧитательГеркин.НаименованиеФункцииПолученияСпискаШагов()));
	Буфер.Добавить(Символы.Таб + "БДД = КонтекстФреймворкаBDD;");
	Буфер.Добавить("");
	Буфер.Добавить(Символы.Таб + "ВсеШаги = Новый Массив;");
	Буфер.Добавить("");

	НаборУжеДобавленныхШагов = Новый Структура;
	РекурсивноЗаписатьОпределенияШаговДляПолученияСпискаТестов(Буфер, Узел, НаборАдресовШагов, НаборУжеДобавленныхШагов);

	Буфер.Добавить("");
	Буфер.Добавить(Символы.Таб + "Возврат ВсеШаги;");
	Буфер.Добавить("КонецФункции");

	Если ЭтоПервичнаяГенерация Тогда
		Буфер.Добавить("");
		Буфер.Добавить("// Реализация шагов");
		Буфер.Добавить("");
	КонецЕсли;

КонецПроцедуры

Процедура ВывестиВБуферСтрокиТаблицы(Буфер, ТаблицаМодуляШагов, Начало, Окончание)
	Для Счетчик = Начало По Окончание Цикл
		Строка = ТаблицаМодуляШагов[Счетчик].Строка;
		Буфер.Добавить(Строка);
	КонецЦикла;
КонецПроцедуры

Процедура ЗаписатьМетодыВФайл(ТаблицаМодуляШагов, НаборМетодовМодуляШагов, Буфер)
	Для каждого ОписаниеМетода Из НаборМетодовМодуляШагов Цикл
		Если ОписаниеМетода.ИмяМетода = ЧитательГеркин.НаименованиеФункцииПолученияСпискаШагов() Тогда
			Продолжить
		КонецЕсли;

		ВывестиВБуферСтрокиТаблицы(Буфер, ТаблицаМодуляШагов, ОписаниеМетода.НомерСтрокиБлокаДоМетода, ОписаниеМетода.НомерСтрокиКонец);
	КонецЦикла;
КонецПроцедуры

Процедура ЗаписатьОставшеесяТелоВФайл(ТаблицаМодуляШагов, НаборМетодовМодуляШагов, Буфер)
	Ожидаем.Что(НаборМетодовМодуляШагов.Количество(), "Ожидали, что НаборМетодовМодуляШагов имеет количество > 0, а это не так").Больше(0);

	ПоследняяСтрока = НаборМетодовМодуляШагов[НаборМетодовМодуляШагов.Количество() - 1];
	ВывестиВБуферСтрокиТаблицы(Буфер, ТаблицаМодуляШагов, ПоследняяСтрока.НомерСтрокиКонец + 1, ТаблицаМодуляШагов.Количество() - 1);
КонецПроцедуры

Процедура ЗаписатьБуферВФайл(Буфер, ФайлДляЗаписи)
	Попытка
		ЗаписьФайла = Новый ЗаписьТекста(ФайлДляЗаписи.ПолноеИмя, "utf-8");

		Для каждого Строка Из Буфер Цикл
			ЗаписьФайла.ЗаписатьСтроку(Строка);
			Лог.Отладка("Записываю в файл шагов ----- "+Строка);
		КонецЦикла;

		ЗаписьФайла.Закрыть();
	Исключение
		ОсвободитьОбъект(ЗаписьФайла);
		ВызватьИсключение;
	КонецПопытки;
КонецПроцедуры

Процедура РекурсивноЗаписатьОпределенияШаговДляПолученияСпискаТестов(Буфер, Узел, НаборАдресовШагов, НаборУжеДобавленныхШагов)
	Лог.Отладка("Обхожу узел "+Узел.ТипШага+": "+Узел.Лексема+" <"+Узел.Тело+">");

	Если Узел.ТипШага = ВозможныеТипыШагов.Шаг Тогда
		Если Не НаборАдресовШагов.Свойство(Узел.АдресШага) И Не НаборУжеДобавленныхШагов.Свойство(Узел.АдресШага) Тогда
			Буфер.Добавить(Символы.Таб + "ВсеШаги.Добавить("""+Узел.АдресШага+""");");
			Лог.Отладка("В НаборУжеДобавленныхШагов добавляю шаг "+Узел.АдресШага);
			Попытка
				НаборУжеДобавленныхШагов.Вставить(Узел.АдресШага);
			Исключение
				Лог.Ошибка("Передан неверный адрес шага "+Узел.АдресШага);
				ВызватьИсключение;
			КонецПопытки;
		КонецЕсли;
	КонецЕсли;

	Для Каждого СтрокаДерева Из Узел.Строки Цикл
		РекурсивноЗаписатьОпределенияШаговДляПолученияСпискаТестов(Буфер, СтрокаДерева, НаборАдресовШагов, НаборУжеДобавленныхШагов);
	КонецЦикла;

КонецПроцедуры

Процедура РекурсивноЗаписатьШагиВФайл(ОписаниеЗаписываемогоФайла, Узел, НаборАдресовШагов)
	Лог.Отладка("Обхожу узел "+Узел.ТипШага+": "+Узел.Лексема+" <"+Узел.Тело+">");

	ФайлШагов = ОписаниеЗаписываемогоФайла.ФайлШагов;

	Если Узел.ТипШага = ВозможныеТипыШагов.Шаг Тогда
		Если НаборАдресовШагов.Свойство(Узел.АдресШага) Тогда
			Лог.Отладка("Шаг уже определен. Пропускаю. Шаг <"+Узел.АдресШага+">");
		Иначе
			ЗаписатьШаг(ОписаниеЗаписываемогоФайла, Узел);
			НаборАдресовШагов.Вставить(Узел.АдресШага, ФайлШагов.ПолноеИмя);
		КонецЕсли;
	КонецЕсли;

	Для Каждого СтрокаДерева Из Узел.Строки Цикл
		РекурсивноЗаписатьШагиВФайл(ОписаниеЗаписываемогоФайла, СтрокаДерева, НаборАдресовШагов);
	КонецЦикла;

КонецПроцедуры

Процедура ЗаписатьШаг(ОписаниеЗаписываемогоФайла, Узел)
	Лог.Отладка("Записываю шаг <"+Узел.АдресШага+">");

	Буфер = ОписаниеЗаписываемогоФайла.Буфер;
	ЭтоПервичнаяГенерация = ОписаниеЗаписываемогоФайла.ЭтоПервичнаяГенерация;
	НаборМетодовМодуляШагов = ОписаниеЗаписываемогоФайла.НаборМетодовМодуляШагов;

	ИмяМетода = Узел.АдресШага;

	Если Не ЭтоПервичнаяГенерация Тогда
		ОписаниеМетода = НаборМетодовМодуляШагов.Найти(ИмяМетода, "ИмяМетода");
		Если ОписаниеМетода <> Неопределено Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;

	СтрокаПараметров = ПолучитьСтрокуПараметров(Узел.Параметры);

	Тело = Узел.Тело;
	Для Счетчик = 1 По СтрЧислоСтрок(Тело) Цикл
		Строка = СтрПолучитьСтроку(Тело, Счетчик);
		СтрокаДляЗаписи = СтрШаблон("//%1", Строка);
		Лог.Отладка("СтрокаДляЗаписи <"+СтрокаДляЗаписи+">");
		Буфер.Добавить(СтрокаДляЗаписи);
	КонецЦикла;

	ШаблонЗаписи = "%1 %2(%3) %4";
	СтрокаДляЗаписи = СтрШаблон(ШаблонЗаписи, "Процедура",  ИмяМетода, СтрокаПараметров, "Экспорт");
	Лог.Отладка("СтрокаДляЗаписи <"+СтрокаДляЗаписи+">");
	Буфер.Добавить(СтрокаДляЗаписи);

	ШаблонЗаписи = "%1 ВызватьИсключение Новый ИнформацияОбОшибке(""Шаг <%3> не реализован"", ""%2"");";
	СтрокаДляЗаписи = СтрШаблон(ШаблонЗаписи, Символы.Таб, ЧитательГеркин.ПараметрИсключенияДляЕщеНеРеализованногоШага(), ИмяМетода);
	Лог.Отладка("СтрокаДляЗаписи <"+СтрокаДляЗаписи+">");
	Буфер.Добавить(СтрокаДляЗаписи);

	СтрокаДляЗаписи = "КонецПроцедуры";
	Лог.Отладка("СтрокаДляЗаписи <"+СтрокаДляЗаписи+">"+Символы.ПС);
	Буфер.Добавить(СтрокаДляЗаписи);
	Буфер.Добавить("");

КонецПроцедуры

Процедура ЗаполнитьНаборАдресовШаговЭкспортнымиМетодами(НаборАдресовШагов, НаборМетодовМодуляШагов)
	Для каждого ОписаниеМетода Из НаборМетодовМодуляШагов Цикл
		НаборАдресовШагов.Вставить(ОписаниеМетода.ИмяМетода);
	КонецЦикла;
КонецПроцедуры

Функция ПолучитьСтрокуПараметров(Знач Параметры)
	СтрокаПараметров = "";
	Если ЗначениеЗаполнено(Параметры) Тогда
		Номер = 1;
		Для Каждого ОписаниеПараметра Из Параметры Цикл
			Лог.Отладка("ОписаниеПараметра.Тип " + ОписаниеПараметра.Тип);
			ПредставлениеПараметра = ПолучитьПредставлениеПараметра(ОписаниеПараметра, Номер);
			СтрокаПараметров = СтрокаПараметров + ПредставлениеПараметра + ",";
			Номер = Номер + 1;
		КонецЦикла;
		СтрокаПараметров = Лев(СтрокаПараметров, СтрДлина(СтрокаПараметров)-1);
 	КонецЕсли;
	Возврат СтрокаПараметров;
КонецФункции // ПолучитьСтрокуПараметров()

Функция ПолучитьПредставлениеПараметра(ОписаниеПараметра, Номер)
	ПредставлениеПараметра = ПредставленияТиповПараметров[ОписаниеПараметра.Тип] + Номер;
	Возврат ПредставлениеПараметра;
КонецФункции // ПолучитьПредставлениеПараметра(ОписаниеПараметра, Номер)

Функция ПолучитьФайлШагов(ФайлФичи)
	Возврат Новый Файл(ОбъединитьПути(ФайлФичи.Путь, "step_definitions", ФайлФичи.ИмяБезРасширения+ ".os"));
КонецФункции // ПолучитьФайлШагов()
//}

Функция Инициализация()
	Лог = Логирование.ПолучитьЛог(ИмяЛога());

	ЧитательГеркин = ЗагрузитьСценарий(ОбъединитьПути(ТекущийСценарий().Каталог, "gherkin-read.os"));

	ДопЛог = Логирование.ПолучитьЛог(ЧитательГеркин.ИмяЛога());
	ДопЛог.УстановитьУровень(Лог.Уровень());

	ВозможныеТипыШагов = ЧитательГеркин.ВозможныеТипыШагов();
	ВозможныеКлючиПараметров = ЧитательГеркин.ВозможныеКлючиПараметров();
	ПредставленияТиповПараметров = ВозможныеПредставленияТиповПараметров();

	РегулярныеВыражения = СоздатьРегулярныеВыражения();
КонецФункции

Функция ВозможныеПредставленияТиповПараметров()
	Рез = Новый Соответствие;
	Рез.Вставить(ВозможныеКлючиПараметров.Строка, "ПарамСтрока");
	Рез.Вставить(ВозможныеКлючиПараметров.Число, "ПарамЧисло");
	Рез.Вставить(ВозможныеКлючиПараметров.Дата, "ПарамДата");
	Рез.Вставить(ВозможныеКлючиПараметров.ПараметрДляТаблицы, "Парам");
	Возврат Рез;
КонецФункции // ВозможныеПредставленияТиповПараметров()

Функция СоздатьРегулярныеВыражения()
	Рез = Новый Структура;
	Рез.Вставить("НачалоЭкспортногоМетода", Новый РегулярноеВыражение("^\s*(Функция|Процедура)\s+([_\w\dа-яё]+)\s*\(.+(Экспорт)+"));
	Рез.Вставить("КонецМетода", Новый РегулярноеВыражение("^\s*(КонецФункции|КонецПроцедуры)"));
	Рез.Вставить("ФункцияПолучитьСписокШагов", Новый РегулярноеВыражение(СтрШаблон("^\s*Функция\s+%1\s*\(.+Экспорт", ЧитательГеркин.НаименованиеФункцииПолученияСпискаШагов())));
	Возврат Новый ФиксированнаяСтруктура(Рез);
КонецФункции
// }

///////////////////////////////////////////////////////////////////
// Точка входа

Инициализация();
