
/////////////////////////////////////////////////////////////////////////////////////////////////
//
// Тест для генератора BDD-шагов сценария в формате OneScript на базе файлов Gherkin
//
/////////////////////////////////////////////////////////////////////////////////////////////////

#Использовать logos
#Использовать asserts

Перем юТест;
Перем Лог;
Перем ЧитательГеркин;

Перем ВозможныеКлючевыеСлова;
Перем ВозможныеТипыШагов;
Перем ВозможныеКлючиПараметров;

Функция ПолучитьСписокТестов(Знач ЮнитТестирование) Экспорт

	юТест = ЮнитТестирование;
		
	МассивТестов = Новый Массив;
	МассивТестов.Добавить("Тест_ДолженПрочитатьПростойФайлФичи");
	МассивТестов.Добавить("Тест_ДолженПрочитатьФайлФичиСоСтроковымиПараметрами");
	
	Возврат МассивТестов;

КонецФункции

Процедура ПослеЗапускаТеста() Экспорт
	ВключитьОтладкуЧитателя(УровниЛога.Информация);
КонецПроцедуры

Процедура Тест_ДолженПрочитатьПростойФайлФичи() Экспорт
	
	// ВключитьОтладкуЧитателя(УровниЛога.Отладка);
	
	ФайлФичи = Новый Файл(ОбъединитьПути(ТекущийСценарий().Каталог, "fixtures\НичегоНеДелаю.feature"));
	
	РезультатыРазбора = ЧитательГеркин.ПрочитатьФайлСценария(ФайлФичи);
	ВозможныеТипыШагов = ЧитательГеркин.ВозможныеТипыШагов();
	
	Ожидаем.Что(РезультатыРазбора, "РезультатыРазбора").ИмеетТип("Структура");
	Ожидаем.Что(РезультатыРазбора.Язык, "Язык").Равно("ru");
	
	ДеревоФич = РезультатыРазбора.ДеревоФич;
	Ожидаем.Что(ДеревоФич, "Ожидали, что дерево фич будет получено как дерево значений, а это не так").ИмеетТип("ДеревоЗначений");
	ДеревоФич = ДеревоФич.Строки[0];
	Ожидаем.Что(ДеревоФич.Строки, "Ожидали, что найдем правильное число фич").ИмеетДлину(1);

	ТелоФункциональности = "Пустой функционал
		|Как Разработчик
		|Я Хочу чтобы файл фичи успешно прочитался";
		
	Ожидаем.Что(ДеревоФич.Лексема, "Ожидали, что правильно нашли ДеревоФич.Лексема, а это не так").Равно(ВозможныеКлючевыеСлова.Функциональность);
	Ожидаем.Что(ДеревоФич.ТипШага, "Ожидали, что правильно нашли ДеревоФич.ТипШага, а это не так").Равно(ВозможныеТипыШагов.Функциональность);
	Ожидаем.Что(ДеревоФич.Тело, "Ожидали, что правильно нашли ДеревоФич.Тело, а это не так").Равно(ТелоФункциональности);
	
	Сценарии = ДеревоФич.Строки;
	Ожидаем.Что(Сценарии, "Ожидали, что найдем правильное число сценариев").ИмеетДлину(1);
	
	Сценарий0 = Сценарии[0];
	Ожидаем.Что(Сценарий0.Лексема, "Ожидали, что правильно нашли Сценарий0.Лексема, а это не так").Равно(ВозможныеКлючевыеСлова.Сценарий);
	Ожидаем.Что(Сценарий0.ТипШага, "Ожидали, что правильно нашли Сценарий0.ТипШага, а это не так").Равно(ВозможныеТипыШагов.Сценарий);
	Ожидаем.Что(Сценарий0.Тело, "Ожидали, что найдем правильное тело сценария 0, а это не так").Равно("Ничего не делаем");

	Шаги = Сценарий0.Строки;
	Ожидаем.Что(Шаги, "Ожидали, что найдем правильное число шагов").ИмеетДлину(2);

	Шаг0 = Шаги[0];
	Ожидаем.Что(Шаг0.Тело, "Ожидали, что найдем правильное тело шага 0, а это не так").Равно("я ничего не делаю");
	Ожидаем.Что(Шаг0.Лексема, "Ожидали, что правильно нашли Шаг0.Лексема, а это не так").Равно(ВозможныеКлючевыеСлова.Когда);
	Ожидаем.Что(Шаг0.ТипШага, "Ожидали, что правильно нашли Шаг0.ТипШага, а это не так").Равно(ВозможныеТипыШагов.Шаг);
	Ожидаем.Что(Шаг0.АдресШага, "Ожидали, что правильно нашли Шаг0.АдресШага, а это не так").Равно("ЯНичегоНеДелаю");

	Шаг1 = Шаги[1];
	Ожидаем.Что(Шаг1.Тело, "Ожидали, что найдем правильное тело шага 1, а это не так").Равно("ничего не происходит");
	Ожидаем.Что(Шаг1.Лексема, "Ожидали, что правильно нашли Шаг1.Лексема, а это не так").Равно(ВозможныеКлючевыеСлова.Тогда);
	Ожидаем.Что(Шаг1.ТипШага, "Ожидали, что правильно нашли Шаг1.ТипШага, а это не так").Равно(ВозможныеТипыШагов.Шаг);
	Ожидаем.Что(Шаг1.АдресШага, "Ожидали, что правильно нашли Шаг1.АдресШага, а это не так").Равно("НичегоНеПроисходит");

	НайденныеЛексемы = РезультатыРазбора.НайденныеЛексемы;
	Ожидаем.Что(НайденныеЛексемы, "Ожидали, что найдем правильное число лексем").ИмеетДлину(4);
	Ожидаем.Что(НайденныеЛексемы[0].Лексема, "Ожидали, что найдем правильное число лексем").Равно(ВозможныеКлючевыеСлова.Функциональность);
	Ожидаем.Что(НайденныеЛексемы[1].Лексема, "Ожидали, что найдем правильное число лексем").Равно(ВозможныеКлючевыеСлова.Сценарий);
	Ожидаем.Что(НайденныеЛексемы[2].Лексема, "Ожидали, что найдем правильное число лексем").Равно(ВозможныеКлючевыеСлова.Когда);
	Ожидаем.Что(НайденныеЛексемы[3].Лексема, "Ожидали, что найдем правильное число лексем").Равно(ВозможныеКлючевыеСлова.Тогда);
КонецПроцедуры

Процедура Тест_ДолженПрочитатьФайлФичиСоСтроковымиПараметрами() Экспорт
	
	ФайлФичи = Новый Файл(ОбъединитьПути(ТекущийСценарий().Каталог, "fixtures\СтроковыеПараметры.feature"));
	
	РезультатыРазбора = ЧитательГеркин.ПрочитатьФайлСценария(ФайлФичи);

	Шаги = РезультатыРазбора.ДеревоФич.Строки[0].Строки[0].Строки;

	Шаг0 = Шаги[0];
	Ожидаем.Что(Шаг0.Тело, "Ожидали, что найдем правильное тело шага 0, а это не так").Равно("я ничего не делаю ""ПарамСтрока""");
	Ожидаем.Что(Шаг0.Лексема, "Ожидали, что правильно нашли Шаг0.Лексема, а это не так").Равно(ВозможныеКлючевыеСлова.Когда);
	Ожидаем.Что(Шаг0.АдресШага, "Ожидали, что правильно нашли Шаг0.АдресШага, а это не так").Равно("ЯНичегоНеДелаю");

	Ожидаем.Что(Шаг0.Параметры, "Ожидали, что найдем правильное число параметров шага 0").ИмеетДлину(1);
	Параметр00 = Шаг0.Параметры[0];
	Ожидаем.Что(Параметр00.Тип, "Ожидали, что найдем правильное значение параметра 0 шага 0, а это не так").Равно(ВозможныеКлючиПараметров.Строка);
	Ожидаем.Что(Параметр00.Значение, "Ожидали, что найдем правильное значение параметра 0 шага 0, а это не так").Равно("ПарамСтрока");

	Шаг1 = Шаги[1];
	Ожидаем.Что(Шаг1.Тело, "Ожидали, что найдем правильное тело шага 1, а это не так").Равно("ничего не происходит 'ДругойПарамСтрока'");
	Ожидаем.Что(Шаг1.Лексема, "Ожидали, что правильно нашли Шаг1.Лексема, а это не так").Равно(ВозможныеКлючевыеСлова.Тогда);
	Ожидаем.Что(Шаг1.АдресШага, "Ожидали, что правильно нашли Шаг1.АдресШага, а это не так").Равно("НичегоНеПроисходит");

	Ожидаем.Что(Шаг1.Параметры, "Ожидали, что найдем правильное число параметров шага 1").ИмеетДлину(1);
	Параметр10 = Шаг1.Параметры[0];
	Ожидаем.Что(Параметр10.Тип, "Ожидали, что найдем правильное значение параметра 0 шага 1, а это не так").Равно(ВозможныеКлючиПараметров.Строка);
	Ожидаем.Что(Параметр10.Значение, "Ожидали, что найдем правильное значение параметра 0 шага 1, а это не так").Равно("ДругойПарамСтрока");

КонецПроцедуры

Процедура ВключитьОтладкуЧитателя(НовыйУровеньЛога)
	ДопЛог = Логирование.ПолучитьЛог(ЧитательГеркин.ИмяЛога());
	ДопЛог.УстановитьУровень(НовыйУровеньЛога);
КонецПроцедуры

////////////////////////////////////////////////////////////////////
// Программный интерфейс

Функция Инициализация() 
	Лог = Логирование.ПолучитьЛог("oscript.app.bdd-tests");
	Лог.УстановитьУровень(УровниЛога.Отладка);

	//КаталогСкрипта = Новый Файл(ТекущийСценарий().Источник).Путь;
	ПодключитьСценарий(ОбъединитьПути(ТекущийСценарий().Каталог, "../src/gherkin-read.os"), "ЧитательGherkin");
	ЧитательГеркин = Новый ЧитательGherkin;

	ВозможныеТипыШагов = ЧитательГеркин.ВозможныеТипыШагов();
	ВозможныеКлючевыеСлова = ЧитательГеркин.ВозможныеКлючевыеСлова();
	ВозможныеКлючиПараметров = ЧитательГеркин.ВозможныеКлючиПараметров();

КонецФункции

///////////////////////////////////////////////////////////////////
// Точка входа

Инициализация();
