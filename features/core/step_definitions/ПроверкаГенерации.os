#Использовать logos

Перем Лог;

Перем БДД;
Перем ФайлИсходнойФичи;
Перем ФайлФичи;

Функция ПолучитьСписокШагов(КонтекстФреймворкаBDD) Экспорт
	БДД = КонтекстФреймворкаBDD;

	ВсеШаги = Новый Массив;

	ВсеШаги.Добавить("ЯПодготовилСпециальнуюТестовуюФичу");
	ВсеШаги.Добавить("ЯЗапустилГенерациюШаговФичи");
	ВсеШаги.Добавить("ЯПолучилСгенерированныйOs_ФайлВОжидаемомКаталоге");
	ВсеШаги.Добавить("СгенерированныйOs_ФайлПрошелПроверкуНаКорректность");

	Возврат ВсеШаги;
КонецФункции

//я подготовил специальную тестовую фичу "ПередачаПараметров"
Процедура ЯПодготовилСпециальнуюТестовуюФичу(ИмяФичи) Экспорт
	Лог = Логирование.ПолучитьЛог("oscript.app.bdd.ПроверкаГенерации.feature");
	//Лог.УстановитьУровень(УровниЛога.Отладка);

	ФайлИсходнойФичи = ПолучитьФайлИсходнойФичи(ИмяФичи);

	ФайлФичи = ПолучитьТестовыйФайлФичи(ИмяФичи);
	Лог.Отладка("Подготовлена тестовая фича "+ФайлФичи.ПолноеИмя);
КонецПроцедуры

//я запустил генерацию шагов фичи "ПередачаПараметров"
Процедура ЯЗапустилГенерациюШаговФичи(ИмяФичи) Экспорт
	ПутьИсполнителяБДД = ОбъединитьПути(ПолучитьКаталогИсходников(), "bdd.os");
	ФайлИсполнителя = Новый Файл(ПутьИсполнителяБДД);
	Ожидаем.Что(ФайлИсполнителя.Существует(), "Ожидаем, что скрипт исполнителя шагов существует, а его нет. "+ФайлИсполнителя.ПолноеИмя).Равно(Истина);

	УстановитьТекущийКаталог(ФайлФичи.Путь);
	Лог.Отладка("ТекущийКаталог "+ТекущийКаталог());

	ИмяЛогаКоманды = ОбъединитьПути(ФайлФичи.Путь, "gen-log.txt");
	СтрокаКоманды = СтрШаблон("cmd /c oscript.exe %4 %1 generate %2 -debug on %3", ПутьИсполнителяБДД, ФайлФичи.Имя,
		СуффиксПеренаправленияВывода(ИмяЛогаКоманды, Ложь), "-encoding=utf-8");
	Лог.Отладка("СтрокаКоманды "+СтрокаКоманды);

	КодВозврата = Неопределено;
	ЗапуститьПриложение(СтрокаКоманды, ФайлФичи.Путь, Истина, КодВозврата);

	ВывестиТекстФайла(ИмяЛогаКоманды);

	Ожидаем.Что(КодВозврата, "Ожидаем, что код возврата равен 0, а это не так").Равно(0);
КонецПроцедуры

//я получил сгенерированный os-файл в ожидаемом каталоге
Процедура ЯПолучилСгенерированныйOs_ФайлВОжидаемомКаталоге() Экспорт
	 ФайлШагов = Новый Файл(ОбъединитьПути(ФайлФичи.Путь, "step_definitions", ФайлФичи.ИмяБезРасширения+".os"));
	 Ожидаем.Что(ФайлШагов.Существует(), "Ожидаем, что файл исполнителя шагов существует, а его нет. "+ФайлШагов.ПолноеИмя).Равно(Истина);
КонецПроцедуры

//проверка поведения фичи "ПередачаПараметров" закончилась со статусом "НеРеализован"
Процедура ПроверкаПоведенияФичиЗакончиласьСоСтатусом(ИмяФичи, ОжидаемыйСтатусВыполненияСтрока) Экспорт
	Контекст = Новый Структура("Контекст", Новый Структура("Журнал", Новый Структура));
	ИсполнительБДД = ЗагрузитьСценарий(ОбъединитьПути(ПолучитьКаталогИсходников(), "bdd-exec.os"), Контекст);

	ОжидаемыйСтатусВыполнения = ИсполнительБДД.ВозможныеСтатусыВыполнения()[ОжидаемыйСтатусВыполненияСтрока];

	РезультатыВыполнения = ИсполнительБДД.ВыполнитьФичу(ФайлФичи);

	Ожидаем.Что(РезультатыВыполнения, "Ожидали, что дерево фич будет получено как дерево значений, а это не так").ИмеетТип("ДеревоЗначений");

	Функциональность0 = РезультатыВыполнения.Строки[0];
	СообщениеОбОшибке = СтрШаблон("Ожидали, что статус выполнения Функциональность0 будет %1, а это не так", ОжидаемыйСтатусВыполнения);
	Ожидаем.Что(Функциональность0.СтатусВыполнения, СообщениеОбОшибке).Равно(ОжидаемыйСтатусВыполнения);

КонецПроцедуры

//я подставил файл шагов с уже реализованными шагами для фичи "ПередачаПараметров"()
Процедура ЯПодставилФайлШаговСУжеРеализованнымиШагамиДляФичи(ИмяФичи) Экспорт
	ИмяИсполнителяШагов = ФайлИсходнойФичи.ИмяБезРасширения+ ".os";
	ИсходныйФайлИсполнителяШагов = Новый Файл(ОбъединитьПути(ФайлИсходнойФичи.Путь, "step_definitions", ИмяИсполнителяШагов ));
	ФайлИсполнителяШагов = Новый Файл(ОбъединитьПути(ФайлФичи.Путь, "step_definitions", ИмяИсполнителяШагов ));

	Если ФайлИсполнителяШагов.Существует() Тогда
		УдалитьФайлы(ФайлИсполнителяШагов.ПолноеИмя);
	КонецЕсли;
	КопироватьФайл(ИсходныйФайлИсполнителяШагов.ПолноеИмя, ФайлИсполнителяШагов.ПолноеИмя);
КонецПроцедуры

Функция ПолучитьТестовыйФайлФичи(ИмяФичи)
	ВременныйКаталогФичи = ПодготовитьТестовыйКаталогСФичей(ФайлИсходнойФичи);

	ФайлФичи = Новый Файл(ОбъединитьПути(ВременныйКаталогФичи.ПолноеИмя, ИмяФичи+".feature"));
	Возврат ФайлФичи;
КонецФункции

// TODO дубль метода с несколькими тестовыми файлами
Функция ПодготовитьТестовыйКаталогСФичей(ФайлИсходнойФичи)
	КаталогФичи = ВременныеФайлы.СоздатьКаталог();
	Лог.Отладка("Использую временный каталог "+КаталогФичи);
	КопироватьФайл(ФайлИсходнойФичи.ПолноеИмя, ОбъединитьПути(КаталогФичи, ФайлИсходнойФичи.Имя ));

	ИмяИсполнителяШагов = ФайлИсходнойФичи.ИмяБезРасширения+ ".os";
	КаталогИсполнителяШагов = ОбъединитьПути(КаталогФичи, "step_definitions" );
	СоздатьКаталог(КаталогИсполнителяШагов);

	ФайлИсполнителяШагов = Новый Файл(ОбъединитьПути(КаталогИсполнителяШагов, ИмяИсполнителяШагов ));

	Ожидаем.Что(ФайлИсполнителяШагов.Существует(), "Ожидаем, что файл исполнителя шагов не существует, а он есть").Равно(Ложь);
	Возврат Новый Файл(КаталогФичи);
КонецФункции

Процедура ВывестиТекстФайла(Знач ИмяФайла, Знач Кодировка = Неопределено)

	Файл = Новый Файл(ИмяФайла);
	Если НЕ Файл.Существует() Тогда
		Лог.Отладка("Не существует лог-файл <"+ИмяФайла+">");
		Возврат;
	КонецЕсли;

	Если Кодировка = Неопределено Тогда
		Кодировка = "utf-8";
	КонецЕсли;

	ЧТ = Новый ЧтениеТекста(ИмяФайла, Кодировка);
	СтрокаФайла = ЧТ.Прочитать();
	ЧТ.Закрыть();

	Лог.Отладка("");
	Лог.Отладка("  ----------------    ----------------    ----------------  ");
	Лог.Отладка(СтрокаФайла);
	Лог.Отладка("  ----------------    ----------------    ----------------  ");
	Лог.Отладка("");

КонецПроцедуры

Функция СуффиксПеренаправленияВывода(Знач ИмяФайлаПриемника, Знач УчитыватьStdErr = Истина)
	Возврат "> """ + ИмяФайлаПриемника + """" + ?(УчитыватьStdErr, " 2>&1", "");
КонецФункции

Функция ПолучитьКаталогИсходников()
	КаталогПроекта = ОбъединитьПути(ТекущийСценарий().Каталог, "..", "..", "..");
	Возврат ОбъединитьПути(КаталогПроекта, "src");
КонецФункции // ПолучитьКаталогИсходников()

Функция ПолучитьФайлИсходнойФичи(ИмяФичи)
	Возврат Новый Файл(ОбъединитьПути(ТекущийСценарий().Каталог, "..", ИмяФичи+".feature"));
КонецФункции // ПолучитьФайлИсходнойФичи()
